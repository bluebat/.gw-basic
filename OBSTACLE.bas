10 '******************************
20 '*                            *
30 '*          OBSTACLE          *
40 '*                            *
50 '*            J.W.L           *
60 '*                            *
70 '******************************
80 KEY OFF:DIM A(16),B(8)
90 V1$=MID$(TIME$,7,2):V2$=MID$(TIME$,4,2)
100 V1=VAL(V1$):V2=VAL(V2$)
110 RANDOMIZE V1*V2
120 '__________ STAGE __________
130 CLS
140 LOCATE 1,1
150 PRINT STRING$(79,"#")
160 FOR I=2 TO 22:LOCATE I,1:PRINT "#":NEXT I
170 PRINT STRING$(79,"#")
180 LOCATE 11,6:PRINT STRING$(50,"#")
190 LOCATE 6,49: PRINT"############"
200 LOCATE 17,49:PRINT"############"
210 X=INT(RND*70):Y=INT(RND*22)
220 IF X<2 OR Y<2 THEN 210
230 LOCATE Y,X:PRINT"@"
240 GET(9*X-9,14*Y-14)-(9*X,14*Y),B
250 LOCATE 11,70:PRINT""
260 LOCATE 12,70:PRINT""
270 Q1=11:Q2=12
280 GET(621,140)-(630,168),A
290 M=1:N=(-1)^V1:T=0
300 ON KEY(11) GOSUB 470
310 ON KEY(14) GOSUB 550
320 '__________ MOVE  BALL __________
330 KEY(11) ON:KEY(14) ON
340 KEY(11) STOP:KEY(14) STOP
350 X=X+M:Y=Y+N
360 X0=9*X-9:Y0=14*Y-14
370 PUT(X0,Y0),B:PUT(X0-9*M,Y0-14*N),B
380 IF Y=23 OR Y=1 THEN 450
390 IF Y=11 THEN IF X>5 AND X<56 THEN 450
400 IF Y=6 OR Y=17 THEN IF X>48 AND X<61 THEN 450
410 IF X=70 THEN IF Y=Q1 OR Y=Q2 THEN M=-1:T=T+1:SOUND 1000,3:GOTO 330
420 IF X=78 THEN 630
430 IF X=1 THEN M=1:SOUND 262,3
440 GOTO 330
450 N=-N:SOUND 262,3:GOTO 430
460 '__________ MOVE  STICK  UP __________
470 KEY(14) STOP
480 IF Q1=1 THEN 330
490 Q2=Q1:Q1=Q1-1
500 Q0=14*Q1-14
510 PUT(621,Q0),A:PUT(621,Q0+14),A
520 KEY(11) OFF
530 RETURN 420
540 '__________ MOVE  STICK  DOWN __________
550 KEY(11) STOP
560 IF Q2=23 THEN 330
570 Q1=Q2:Q2=Q2+1
580 Q0=14*Q1-14
590 PUT(621,Q0),A:PUT(621,Q0-14),A
600 KEY(14) OFF
610 RETURN 420
620 '__________ END __________
630 PLAY "O4 ML L32 BA#AG#GF#FED#DC#CO3BA#AG#GF#FED#DC#CP8O4C16"
640 OPEN "RECORD" FOR INPUT AS #1:INPUT #1,T0,TH:CLOSE #1
650 LOCATE 12,33:PRINT"YOU MISSED!"
660 IF T>T0 THEN T0=T:GOTO 730
670 LOCATE 14,34:PRINT T;"TIMES"
680 LOCATE 10,29:PRINT"RECORD---";T0;"TIMES"
690 LOCATE 21,15:PRINT"Press any key to continue ,Esc to end "STRING$(5,249)
700 Z$=INPUT$(1):IF Z$<>CHR$(27) THEN 130
710 KEY ON : END
720 '__________ BREAK RECORD __________
730 PLAY"P8"
740 OPEN "RECORD" FOR OUTPUT AS #1:WRITE #1,T0,TH:CLOSE #1:GOTO 670
