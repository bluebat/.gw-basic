10 '*************************
20 '*                       *
30 '*          JUMP         *
40 '*                       *
50 '*          J.W.L        *
60 '*                       *
70 '*************************
80 KEY OFF
90 DEF FNSX(Q)=Q/50+3:DEF FNSY(Q)=Q/60+2
100 DEF FNL(U)=BX(U)/50-BY(U)/60
110 CLS
120 DIM W(82),B(82),P(20,8)
130 O$="":T=0
140 YF=20:YL=260
150 FOR J0=1 TO 2
160 FOR J1=1 TO 5
170 LINE(80+(J1-1)*100,YF)-(80+(J1+1)*100,YL)
180 NEXT J1
190 SWAP YF,YL
200 NEXT J0
210 LINE(180,20)-(130,80):LINE(580,20)-(630,80):                                    LINE(180,260)-(130,200):LINE(580,260)-(630,200)
220 DRAW"BM80,20R600 BM-50,60L500 BM+50,60R400 BM+50,60L500 BM-50,60R600"
230 CIRCLE(80,320),30,,,,.66:CIRCLE(680,320),30,,,,.66:PAINT(680,320)
240 GET(50,300)-(110,340),W:GET(650,300)-(710,340),B
250 FOR J2=1 TO 2
260 FOR J3=1 TO 6
270 ON J2 GOTO 280,320
280 READ WX(J3),WY(J3)
290 PUT(WX(J3),WY(J3)),W
300 PLAY"MS L32 N20"
310 P(FNSX(WX(J3)),FNSY(WY(J3)))=1:GOTO 360
320 READ BX(J3),BY(J3)
330 PUT(BX(J3),BY(J3)),B
340 PLAY"MS L32 N20"
350 P(FNSX(BX(J3)),FNSY(BY(J3)))=1
360 NEXT J3,J2
370 FOR J10=10 TO 30:PLAY"MB ML L32;N=J10;":NEXT J10:PLAY"L8 N0":SOUND 500,5
380 FOR J5=1 TO 4
390 READ EY:FOR EX=0 TO 20:P(EX,EY)=1:NEXT EX
400 NEXT J5
410 FOR J8=1 TO 20:READ EX,EY:P(EX,EY)=1:NEXT J8
420 LOCATE 10,1:PRINT"NUMBER(1-6)":LOCATE 11,5:PRINT"or":LOCATE 12,1:               PRINT"MOVE(WEADZX)(1-2)"
430 '
440 K=0:LOCATE 11,70:PRINT SPC(10):LOCATE 11,70:INPUT N$
450 IF N$="0" THEN RESTORE:ERASE W,B,P:GOTO 110
460 IF VAL(N$)<>0 THEN GOSUB 1110:GOTO 440
470 K=K+2:IF K>LEN(N$) THEN 630
480 A$=MID$(N$,K-1,1):A=VAL(MID$(N$,K,1))
490 IF A<>1 AND A<>2 THEN 440
500 WX0(N)=WX(N):WY0(N)=WY(N)
510 IF A$="W" OR A$="w" THEN WX(N)=WX(N)-50*A:WY(N)=WY(N)-60*A
520 IF A$="E" OR A$="e" THEN WX(N)=WX(N)+50*A:WY(N)=WY(N)-60*A
530 IF A$="A" OR A$="a" THEN WX(N)=WX(N)-100*A
540 IF A$="D" OR A$="d" THEN WX(N)=WX(N)+100*A
550 IF A$="Z" OR A$="z" THEN WX(N)=WX(N)-50*A:WY(N)=WY(N)+60*A
560 IF A$="X" OR A$="x" THEN WX(N)=WX(N)+50*A:WY(N)=WY(N)+60*A
570 IF P(FNSX(WX(N)),FNSY(WY(N)))=1 THEN WX(N)=WX0(N):WY(N)=WY0(N):GOTO 440
580 PUT(WX0(N),WY0(N)),W
590 GOSUB 1120
600 P(FNSX(WX(N)),FNSY(WY(N)))=1:P(FNSX(WX0(N)),FNSY(WY0(N)))=0
610 IF A<>1 THEN 470
620 '
630 T=T+1:LOCATE 13,76:PRINT T
640 '
650 FOR I=1 TO 6
660 C(I)=0:H2=0
670 SX=FNSX(BX(I)):SY=FNSY(BY(I))
680 IF P(SX-1,SY+1)=1 AND P(SX-2,SY+2)=0 THEN C(I)=C(I)+20:SX=SX-2:SY=SY+2:         Q$="Y":GOTO 680
690 IF P(SX-2,SY)=1 AND P(SX-4,SY)=0 THEN C(I)=C(I)+20:SX=SX-4:Q$="Y":GOTO 680
700 IF Q$="Y" THEN GOTO 770
710 IF P(SX-1,SY+1)=1 AND P(SX-2,SY+2)=1 AND P(SX-2,SY)=0 THEN C(I)=C(I)+10:        SX=SX-2:GOTO 800
720 IF P(SX-2,SY)=1 AND P(SX-4,SY)=1 AND P(SX-1,SY+1)=0 THEN C(I)=C(I)+10:          SX=SX-1:SY=SY+1:GOTO 800
730 IF P(SX-1,SY+1)=0 AND P(SX-2,SY)=0 THEN SX=SX-2:C(I)=C(I)+10:GOTO 800
740 IF SY=2 AND SX=4 OR SY=3 AND SX=5 THEN IF P(SX+1,SY+1)=0 THEN C(I)=C(I)+15:     SX=SX+1:SY=SY+1:GOTO 800
750 IF SY=4 THEN IF P(SX+1,SY+1)=0 THEN SX=SX+1:SY=SY+1:C(I)=C(I)+5:GOTO 800
760 IF SY>4 AND SX<11 THEN IF P(SX-1,SY-1)=0 THEN SX=SX-1:SY=SY-1:C(I)=C(I)+5:      GOTO 800
770 IF P(SX+1,SY+1)=1 AND P(SX+2,SY+2)=0 AND SX=8 AND SY=6 THEN C(I)=C(I)+10:       SX=SX+2:SY=SY+2:Q$="Y":H2=1:GOTO 680
780 IF P(SX+1,SY+1)=1 AND P(SX+2,SY+2)=0 THEN C(I)=C(I)+2:SX=SX+2:SY=SY+2:          Q$="Y":H2=1:GOTO 680
790 IF P(SX-1,SY-1)=1 AND P(SX-2,SY-2)=0 AND H2=0 AND SX>7 THEN C(I)=C(I)+1:        SX=SX-2:SY=SY-2:Q$="Y":GOTO 680
800 IF Q$="Y" THEN Q$="N"
810 IF C(I)=10 THEN C(I)=4:GOTO 770
820 IF POINT(BX(I)-70,BY(I)+20)=1 OR POINT(BX(I)-20,BY(I)+80)=1 THEN                C(I)=C(I)-1
830 IF C(I)<>0 THEN C(I)=C(I)+(FNL(I))^2
840 NEXT I
850 '
860 FOR J9=5 TO 1 STEP -1
870 IF C(J9)>=C(J9+1) THEN 890
880 SWAP BX(J9),BX(J9+1):SWAP BY(J9),BY(J9+1):SWAP C(J9),C(J9+1)
890 NEXT J9
900 '
910 I=1:H1=0
920 IF T=1 THEN I=3
930 IF T=2 THEN I=6
940 '
950 SX=FNSX(BX(I)):SY=FNSY(BY(I))
960 IF P(SX-1,SY+1)=1 AND P(SX-2,SY+2)=0 THEN GOSUB 1170:BX(I)=BX(I)-100:           BY(I)=BY(I)+120:P(SX,SY)=0:P(SX-2,SY+2)=1:GOSUB 1160:O$="Y":GOTO 950
970 IF P(SX-2,SY)=1 AND P(SX-4,SY)=0 THEN GOSUB 1170:BX(I)=BX(I)-200:P(SX,SY)=0:    P(SX-4,SY)=1:GOSUB 1160:O$="Y":GOTO 950
980 IF O$="Y" THEN 1050
990 IF P(SX-2,SY)=1 AND P(SX-4,SY)=1 AND P(SX-1,SY+1)=0 THEN GOSUB 1170:            BX(I)=BX(I)-50:BY(I)=BY(I)+60:P(SX,SY)=0:P(SX-1,SY+1)=1:GOSUB 1160:GOTO 440
1000 IF P(SX-1,SY+1)=1 AND P(SX-2,SY+2)=1 AND P(SX-2,SY)=0 THEN GOSUB 1170:          BX(I)=BX(I)-100:P(SX,SY)=0:P(SX-2,SY)=1:GOSUB 1160:GOTO 440
1010 IF P(SX-1,SY+1)=0 AND P(SX-2,SY)=0 THEN GOSUB 1170:BX(I)=BX(I)-50:              BY(I)=BY(I)+60:P(SX,SY)=0:P(SX-1,SY+1)=1:GOSUB 1160:O$="N":GOTO 440
1020 IF SY=2 AND SX=4 OR SY=3 AND SX=5 THEN IF P(SX+1,SY+1)=0 THEN GOSUB 1170:       BX(I)=BX(I)+50:BY(I)=BY(I)+60:P(SX,SY)=0:P(SX+1,SY+1)=1:GOSUB 1160:GOTO 440
1030 IF SY=4 THEN IF P(SX+1,SY+1)=0 THEN GOSUB 1170:BX(I)=BX(I)+50:                  BY(I)=BY(I)+60:P(SX,SY)=0:P(SX+1,SY+1)=1:GOSUB 1160:GOTO 440
1040 IF SY>4 AND SX<11 THEN IF P(SX-1,SY-1)=0 THEN GOSUB 1170:BX(I)=BX(I)-50:        BY(I)=BY(I)-60:P(SX,SY)=0:P(SX-1,SY-1)=1:GOSUB 1160:GOTO 440
1050 SX=FNSX(BX(I)):SY=FNSY(BY(I))
1060 IF P(SX+1,SY+1)=1 AND P(SX+2,SY+2)=0 THEN GOSUB 1170:BX(I)=BX(I)+100:           BY(I)=BY(I)+120:P(SX,SY)=0:P(SX+2,SY+2)=1:GOSUB 1160:O$="Y":H1=1:GOTO 950
1070 IF P(SX-1,SY-1)=1 AND P(SX-2,SY-2)=0 AND SX>7 AND H1=0 THEN GOSUB 1170:         BX(I)=BX(I)-100:BY(I)=BY(I)-120:P(SX,SY)=0:P(SX-2,SY-2)=1:GOSUB 1160:O$="Y"     :GOTO 950
1080 IF O$="Y" THEN O$="N":GOTO 440
1090 GOTO 1200
1100 '
1110 N=VAL(N$):PUT(WX(N),WY(N)),W:GOTO 1130
1120 SOUND 800,6
1130 FOR J4=1 TO 11:PUT(WX(N),WY(N)),W:NEXT J4
1140 RETURN
1150 '
1160 SOUND 800,6
1170 FOR J7=1 TO 11:PUT(BX(I),BY(I)),B:NEXT J7
1180 RETURN
1190 '
1200 LOCATE 10,35:PRINT"I am the winner!"
1210 LOCATE 11,28:INPUT "Do you want to play again(y/n)";D$
1220 IF D$="Y" OR D$="y" THEN RESTORE:ERASE B,W,P:GOTO 110
1230 END
1240 DATA 50,0,100,60,150,0,250,0,200,60,150,120,                                         650,0,600,60,550,0,450,0,500,60,550,120
1250 DATA 0,1,7,8
1260 DATA 0,2,2,2,1,3,3,3,2,4,4,4,1,5,3,5,0,6,2,6
1270 DATA 18,2,20,2,17,3,19,3,16,4,18,4,17,5,19,5,18,6,20,6
