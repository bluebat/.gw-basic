10 '************************
20 '*                      *
30 '*  REACTION    J.W.L   *
40 '*                      *
50 '************************
60 DIM N(20)
70 RANDOMIZE VAL(RIGHT$(TIME$,2))
80 ON KEY(12) GOSUB 270
90 ON KEY(13) GOSUB 390
100 GOTO 470
110 KEY(12) ON:KEY(13) ON
120 LOCATE 13,30:PRINT 0
130 '__________ print __________
140 FOR I=1 TO 120:NEXT
150 N(0)=INT(RND*10)
160 FOR I=0 TO J
170 LOCATE 13,40+I:PRINT CHR$(N(I)+48)
180 NEXT
190 LOCATE 13,41+J:PRINT SPC(50)
200 J=J+1
210 IF ANS$="wrong" THEN PASS=PASS+1
220 IF PASS=3 THEN PASS=0:ANS$="":KEY(13) ON
230 IF J=15 THEN 460
240 FOR I=J TO 1 STEP -1:N(I)=N(I-1):NEXT
250 GOTO 140
260 '__________ kill __________
270 KEY(13) STOP
280 K0=SCREEN(13,31):K=K0-48
290 FOR F=J TO 0 STEP -1
300 IF K=N(F) THEN 340
310 NEXT F
320 ANS$="wrong"
330 GOTO 370
340 SOUND 575,.5:T=T+1:J=J-1
350 FOR I=F TO J:N(I)=N(I+1):NEXT
360 KEY(13) ON
370 RETURN
380 '__________ add __________
390 KEY(12) STOP
400 S=S+1
410 IF S=10 THEN S=0
420 LOCATE 13,30:PRINT S
430 KEY(12) ON
440 RETURN
450 '__________ end __________
460 PLAY"MLO3C8":PLAY"MBMLO3C2":PLAY"MLO3E4":PLAY"MBMLO3E4":PLAY"MLO3F8"
470 CLS:KEY OFF
480 LOCATE 2,10:PRINT"Press SPACE to continue , others to end"
490 PRINT CHR$(31)SPC(30)"Press "CHR$(26)" to add , "CHR$(27)" to kill"
500 LOCATE 20,36:PRINT T;"TIMES"
510 T=0:S=0:J=0
520 D$=INPUT$(1):D=ASC(D$)
530 IF D=32 THEN 110
540 KEY ON:END
